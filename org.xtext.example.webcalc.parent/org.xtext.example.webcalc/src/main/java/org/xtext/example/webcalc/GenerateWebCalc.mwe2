/**
 * Copyright (c) 2016 TypeFox GmbH (http://typefox.io)
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 */
module org.xtext.example.webcalc.GenerateWebCalc

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "org.xtext.example.webcalc"
var fileHeader = "/**
 * Copyright (c) 2016 TypeFox GmbH (http://typefox.io)
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 */"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				web = {
					enabled = true
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = fileHeader
			}
		}
		language = StandardLanguage {
			name = "org.xtext.example.webcalc.WebCalc"
			fileExtensions = "webcalc"
			
			scopeProvider = {
				generateStub = false
			}
			serializer = {
				generateStub = false
			}
			generator = {
				generateStub = false
			}
			webSupport = {
				framework = "Ace"
				highlightingPath = "js/xtext/mode-webcalc.js"
				generateHtmlExample = false
			}
		}
	}
}
